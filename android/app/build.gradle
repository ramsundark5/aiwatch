apply plugin: "com.android.application"

project.ext.envConfigFiles = [
        debug: ".env.dev",
        release: ".env.lite",
        releasePro: ".env.pro",
]
apply from: project(':react-native-config').projectDir.getPath() + "/dotenv.gradle"

import com.android.build.OutputFile

project.ext.react = [
    entryFile: "index.js"
]

apply from: "../../node_modules/react-native/react.gradle"
apply from: '../../node_modules/react-native-unimodules/gradle.groovy'

/**
 * Set this to true to create two separate APKs instead of one:
 *   - An APK that only works on ARM devices
 *   - An APK that only works on x86 devices
 * The advantage is the size of the APK is reduced by about 4MB.
 * Upload all the APKs to the Play Store and people will download
 * the correct one based on the CPU architecture of their device.
 */
def enableSeparateBuildPerCPUArchitecture = true

/**
 * Run Proguard to shrink the Java bytecode in release builds.
 */
def enableProguardInReleaseBuilds = true

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    packagingOptions {
        pickFirst 'lib/**/*.so'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }

    defaultConfig {
        applicationId 'com.aiwatch'
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 2
        versionName "1.0.1"
        manifestPlaceholders = [
            appAuthRedirectScheme: 'com.aiwatch'
        ]
        resValue "string", "build_config_package", "com.aiwatch"
    }
    signingConfigs {
        release {
            if (project.hasProperty('AIWATCH_UPLOAD_STORE_FILE')) {
                storeFile file(AIWATCH_UPLOAD_STORE_FILE)
                storePassword AIWATCH_UPLOAD_STORE_PASSWORD
                keyAlias AIWATCH_UPLOAD_KEY_ALIAS
                keyPassword AIWATCH_UPLOAD_KEY_PASSWORD
            }
        }
        releasePro {
            if (project.hasProperty('AIWATCH_UPLOAD_STORE_FILE')) {
                storeFile file(AIWATCH_UPLOAD_STORE_FILE)
                storePassword AIWATCH_UPLOAD_STORE_PASSWORD
                keyAlias AIWATCH_UPLOAD_KEY_ALIAS
                keyPassword AIWATCH_UPLOAD_KEY_PASSWORD
            }
        }
    }

    splits {
        abi {
            reset()
            enable enableSeparateBuildPerCPUArchitecture
            universalApk false  // If true, also generate a universal APK
            include "armeabi-v7a", "x86", "arm64-v8a", "x86_64"
        }
    }
    buildTypes {
        release {
            minifyEnabled enableProguardInReleaseBuilds
            signingConfig signingConfigs.release
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
        releasePro {
            minifyEnabled enableProguardInReleaseBuilds
            signingConfig signingConfigs.releasePro
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            applicationIdSuffix ".pro"
        }
    }
    aaptOptions {
        noCompress "tflite"
        ignoreAssetsPattern "!*ffprobe"
    }
    // applicationVariants are e.g. debug, release
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            // For each separate APK per architecture, set a unique version code as described here:
            // http://tools.android.com/tech-docs/new-build-system/user-guide/apk-splits
            def versionCodes = ["armeabi-v7a":1, "x86":2, "arm64-v8a": 3, "x86_64": 4]
            def abi = output.getFilter(OutputFile.ABI)
            if (abi != null) {  // null for the universal-debug, universal-release variants
                output.versionCodeOverride =
                        versionCodes.get(abi) * 1048576 + defaultConfig.versionCode
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:appcompat-v7:${rootProject.ext.supportLibVersion}"
    implementation "com.android.support:support-compat:${rootProject.ext.supportLibVersion}"
    implementation "com.android.support:support-media-compat:${rootProject.ext.supportLibVersion}"
    implementation 'com.facebook.react:react-native:+'

    //common libs
    implementation 'com.google.http-client:google-http-client-gson:1.26.0'
    implementation('com.google.api-client:google-api-client-android:1.26.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    implementation('com.google.apis:google-api-services-drive:v3-rev136-1.25.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    implementation 'org.apache.commons:commons-collections4:4.3'
    implementation 'com.google.code.gson:gson:2.8.5'

    //tflite
    implementation 'org.tensorflow:tensorflow-lite:1.13.1'

    //javacv and ffmpeg
    implementation 'nl.bravobit:android-ffmpeg:1.1.5'
    implementation 'org.bytedeco:javacv:1.4.4'
    implementation 'org.bytedeco.javacpp-presets:ffmpeg:4.1-1.4.4:android-arm'
    implementation 'org.bytedeco.javacpp-presets:ffmpeg:4.1-1.4.4:android-arm64'
    implementation 'org.bytedeco.javacpp-presets:ffmpeg:4.1-1.4.4:android-x86'
    implementation 'org.bytedeco.javacpp-presets:ffmpeg:4.1-1.4.4:android-x86_64'

    //firebase
    implementation 'com.google.firebase:firebase-core:16.0.9'
    implementation 'com.google.firebase:firebase-perf:17.0.2'
    implementation 'com.google.firebase:firebase-messaging:18.0.0'
    implementation 'com.google.firebase:firebase-auth:17.0.0'
    implementation 'com.google.firebase:firebase-firestore:19.0.1'
    implementation 'com.google.android.gms:play-services-auth:16.0.1'

    //react native projects
    implementation project(':react-native-screens')
    implementation project(':react-native-gesture-handler')
    implementation project(':react-native-yz-vlcplayer')
    implementation project(':react-native-vector-icons')
    implementation project(':react-native-orientation')
    implementation project(':react-native-google-signin')
    implementation project(':bugsnag-react-native')
    implementation project(':react-native-config')
    implementation project(path: ':transcoder')
    addUnimodulesDependencies()
}

repositories {
    maven {
        url 'https://google.bintray.com/tensorflow'
    }
}

// Run this once to be able to run the application with BUCK
// puts all compile dependencies into folder libs for BUCK to use
task copyDownloadableDepsToLibs(type: Copy) {
    from configurations.compile
    into 'libs'
}

// import DownloadModels task
project.ext.ASSET_DIR = projectDir.toString() + '/src/main/assets'
project.ext.TMP_DIR   = project.buildDir.toString() + '/downloads'

// Download default models; if you wish to use your own models then
// place them in the "assets" directory and comment out this line.
//apply from: "download-models.gradle"

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'io.objectbox' // apply last